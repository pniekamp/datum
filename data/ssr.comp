#version 440 core
#include "bound.glsl"
#include "transform.glsl"
#include "camera.glsl"
#include "gbuffer.glsl"
#include "lighting.glsl"

layout(local_size_x = 16, local_size_y = 16) in;
layout(local_size_x_id = 1, local_size_y_id = 2) in;

layout(set=0, binding=0, std430, row_major) readonly buffer SceneSet 
{
  mat4 proj;
  mat4 invproj;
  mat4 view;
  mat4 invview;
  mat4 worldview;
  mat4 orthoview;
  mat4 prevview;
  mat4 skyview;
  vec4 viewport;

  Camera camera;

} scene;

layout(set=0, binding=5) uniform sampler2D colormap;
layout(set=0, binding=6) uniform sampler2D diffusemap;
layout(set=0, binding=7) uniform sampler2D specularmap;
layout(set=0, binding=8) uniform sampler2D normalmap;
layout(set=0, binding=9) uniform sampler2D depthmap;
layout(set=0, binding=10) uniform sampler2D depthmipmap;
layout(set=0, binding=18) uniform sampler2DArray envbrdfmap;

layout(set=0, binding=37) writeonly uniform image2D dest;

const float Jitter = 1.0;
const float Thickness = 0.05f;
const float MaxSteps = 350.0;
const float MaxDistance = 24.0;

///////////////////////// traceray //////////////////////////////////////////
// http://casual-effects.blogspot.com.au/2014/08/screen-space-ray-tracing.html
bool traceray(vec3 origin, vec3 direction, mat4 proj, vec2 viewport, out vec2 hitpixel, out vec3 hitpoint, inout float stepcount)
{
  bool result = false;

  vec3 endpoint = origin + direction * MaxDistance;

  vec4 H0 = proj * vec4(origin, 1);
  vec4 H1 = proj * vec4(endpoint, 1);

  float k0 = 1 / H0.w;
  float k1 = 1 / H1.w;

  vec3 Q0 = origin * k0;
  vec3 Q1 = endpoint * k1;

  vec2 P0 = (0.5 + 0.5 * H0.xy * k0) * viewport;
  vec2 P1 = (0.5 + 0.5 * H1.xy * k1) * viewport;

  P1 += (distance(P0, P1) < 0.01) ? 0.01 : 0.0;
  
  vec2 delta = P1 - P0;

  bool permute = false;
  if(abs(delta.x) < abs(delta.y))
  {
    // This is a more-vertical line
    permute = true;
    delta = delta.yx;
    P0 = P0.yx;
    P1 = P1.yx;
  } 

  float stepdir = sign(delta.x);
  float invdx = stepdir / delta.x;

  vec3 dQ = (Q1 - Q0) * invdx;
  float dk = (k1 - k0) * invdx;
  vec2 dP = vec2(stepdir, delta.y * invdx);

  P0 += dP * Jitter;
  Q0 += dQ * Jitter; 
  k0 += dk * Jitter;

  float k = k0 + stepcount*dk;
  float end = clamp(P1.x, 0, permute ? viewport.y - 1 : viewport.x - 1) * stepdir;
  
  vec2 P = P0 + stepcount*dP;
  vec3 Q = Q0 + vec3(0, 0, stepcount*dQ.z);

  int level = 1;
  float rayzmin = Q.z / k;
  float rayzmax = rayzmin;

  while (P.x * stepdir <= end && stepcount < MaxSteps && level > 0)
  {
    int multiplier = (1 << level);

#if 1 // accurate
    float a = floor(P.x/multiplier)*multiplier + ((delta.x > 0) ? multiplier : -1);
    float b = floor(P.y/multiplier)*multiplier + ((delta.y > 0) ? multiplier : -1);
    multiplier = clamp(int(min((a - floor(P.x))/dP.x, (b - floor(P.y))/dP.y)), 1, multiplier);  
#endif

    rayzmin = rayzmax;
    rayzmax = (Q.z + multiplier * dQ.z) / (k + multiplier * dk);

    hitpixel = permute ? P.yx : P.xy;

    vec2 depths = texelFetch(depthmipmap, ivec2(hitpixel) >> level, (level-1)).rg;

    float scenezmin = -depths.r;
    float scenezmax = -depths.g;

    if (rayzmax <= scenezmin && scenezmax - Thickness < rayzmin)
    {
      multiplier = 0;    
      rayzmax = rayzmin;
      --level;

      if (level == 0)
      {
        level = 2;
        for(int i = 0; P.x * stepdir <= end && stepcount < MaxSteps && i < 4; ++i)
        {
          rayzmin = rayzmax;
          rayzmax = (Q.z + 0.5 * dQ.z) / (k + 0.5 * dk);

          hitpixel = permute ? P.yx : P.xy;

          scenezmax = -view_depth(scene.proj, texelFetch(depthmap, ivec2(hitpixel), 0).r);

          if (rayzmax <= scenezmax && scenezmax - Thickness < rayzmin)
          {
            result = (stepcount > 0);
            level = 0;
            break;
          }

          P += dP; Q.z += dQ.z; k += dk; stepcount += 1;
        }
      }
    }
    else
    {
      if (floor(P.x/(1<<level+1)) != floor((P.x + multiplier*dP.x)/(1<<level+1)))
      {
        level = min(level + 1, 6);
      }
    }

    P += multiplier*dP; Q.z += multiplier*dQ.z; k += multiplier*dk; stepcount += multiplier;
  }

  Q.xy += dQ.xy * stepcount;

  hitpoint = Q * (1 / k);
  
  return result;
}
 
///////////////////////// main //////////////////////////////////////////////
void main()
{ 
  gl_WorkGroupSize;
  ivec2 xy = ivec2(gl_GlobalInvocationID.xy);
  ivec2 fbosize = textureSize(depthmap, 0).xy;
  
  float depth = texelFetch(depthmap, xy, 0).r;
  
  vec3 fragcolor = vec3(0);

  vec4 srccolor = texelFetch(colormap, xy, 0);
  vec4 specular = texelFetch(specularmap, xy, 0);

  if (depth > 1 - 0.998 && specular.w < 0.4 && srccolor.a < 0.85)
  {
    vec3 position = view_position(scene.proj, scene.invproj, xy, fbosize, depth);
    vec3 normal = view_normal(scene.view, texelFetch(normalmap, xy, 0).xyz);
    vec3 eyevec = normalize(-position + vec3(0, 0.5, 0)); // fudge for grazing angles

//    vec3 direction = normalize(reflect(-eyevec, normal));
    vec3 direction = specular_dominantdirection(normal, reflect(-eyevec, normal), specular.w);

    float stepcount = 0;
    vec2 hitpixel = vec2(0);
    vec3 hitpoint  = vec3(0);
    
    if (traceray(position, direction, scene.proj, fbosize, hitpixel, hitpoint, stepcount))
    {
      float edgefade = 1 - 10 * max(max(abs(hitpixel.x / fbosize.x - 0.5), abs(hitpixel.y / fbosize.y - 0.5)) - 0.4, 0);
      float depthfade = 1 - 1000 * max((1 - depth) - 0.997, 0);
      float distancefade = 1 - min(max(stepcount / MaxSteps, distance(hitpoint, position) / MaxDistance), 1);
      float anglefade = 10 * clamp(-direction.z, 0, 0.1);
      float roughnessfade = 1 - min(2.5 * specular.w, 1);

      vec4 hitcolor = texelFetch(colormap, ivec2(hitpixel), 0);
      //vec4 hitcolor = textureLod(colormap, hitpixel / textureSize(colormap, 0).xy, specular.w * (2/0.4)); // requires blured colorbuffer

      vec2 envbrdf = texture(envbrdfmap, vec3(dot(normal, eyevec), specular.w, 0)).rg;

      fragcolor = depthfade * edgefade * distancefade * anglefade * roughnessfade * (1 - srccolor.a) * (1 - hitcolor.a) * hitcolor.rgb * (envbrdf.x*specular.rgb + envbrdf.y);
    }
  }

  imageStore(dest, xy, vec4(fragcolor, 1));
}

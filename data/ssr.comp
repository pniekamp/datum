#version 450 core
#include "gbuffer.glsl"
#include "transform.glsl"
#include "lighting.glsl"

layout(local_size_x = 16, local_size_y = 16) in;
layout(local_size_x_id = 1, local_size_y_id = 2) in;

layout(std430, set=0, binding=0, row_major) buffer SceneSet 
{
  mat4 proj;
  mat4 invproj;
  mat4 view;
  mat4 invview;
  mat4 worldview;

} scene;

layout(set=0, binding=1) uniform sampler2D rt0map;
layout(set=0, binding=2) uniform sampler2D rt1map;
layout(set=0, binding=3) uniform sampler2D normalmap;
layout(set=0, binding=4) uniform sampler2D depthmap;
layout(set=3, binding=1) uniform sampler2D colormap;

layout(set=3, binding=13) writeonly uniform image2D dest;

const float Stride = 1.0;
const float Jitter = 1.0;
const float Multiplier = 8.0;
const float Thickness = 0.05f;
const float MaxSteps = 350.0;
const float MaxDistance = 2.5;

///////////////////////// traceray //////////////////////////////////////////
// http://casual-effects.blogspot.com.au/2014/08/screen-space-ray-tracing.html
bool traceray(vec3 origin, vec3 direction, mat4 proj, out vec2 hitpixel, out vec3 hitpoint, out float stepcount)
{
  stepcount = 0;

  vec3 endpoint = origin + direction * MaxDistance;

  vec4 H0 = proj * vec4(origin, 1);
  vec4 H1 = proj * vec4(endpoint, 1);

  float k0 = 1 / H0.w;
  float k1 = 1 / H1.w;

  vec3 Q0 = origin * k0;
  vec3 Q1 = endpoint * k1;

  vec2 P0 = H0.xy * k0;
  vec2 P1 = H1.xy * k1;

  P1 += (distance(P0, P1) < 0.01) ? 0.01 : 0.0;
  
  vec2 delta = P1 - P0;

  bool permute = false;
  if(abs(delta.x) < abs(delta.y))
  {
    // This is a more-vertical line
    permute = true;
    delta = delta.yx;
    P0 = P0.yx;
    P1 = P1.yx;
  }

  float stepdir = sign(delta.x);
  float invdx = stepdir / delta.x;

  vec3 dQ = (Q1 - Q0) * invdx;
  float dk = (k1 - k0) * invdx;
  vec2 dP = vec2(stepdir, delta.y * invdx);

  dP *= Stride;
  dQ *= Stride;
  dk *= Stride;

  P0 += dP * Jitter;
  Q0 += dQ * Jitter; 
  k0 += dk * Jitter;

  float k = k0;
  float end = P1.x * stepdir;
    
  vec2 P = P0;
  vec3 Q = Q0;

  while (P.x * stepdir <= end && stepcount < MaxSteps-Multiplier)
  { 
    P += Multiplier*dP; Q.z += Multiplier*dQ.z; k += Multiplier*dk; stepcount += Multiplier;

    hitpixel = permute ? P.yx : P.xy;

    float scenezmax = -view_depth(scene.proj, texelFetch(depthmap, ivec2(hitpixel), 0).r);
    
    if (Q.z / k <= scenezmax)
      break;
  }
 
  P -= Multiplier*dP; Q.z -= Multiplier*dQ.z; k -= Multiplier*dk; stepcount -= Multiplier;

  bool result = false;
  float rayzmin = Q.z / k;
  float rayzmax = rayzmin;

  while (P.x * stepdir <= end && stepcount < MaxSteps)
  {  
    rayzmin = rayzmax;
    rayzmax = (Q.z + 0.5f * dQ.z) / (k + 0.5f * dk);

    hitpixel = permute ? P.yx : P.xy;

    float scenezmax = -view_depth(scene.proj, texelFetch(depthmap, ivec2(hitpixel), 0).r);

    if (rayzmax <= scenezmax && rayzmin > scenezmax - Thickness)
    {
      result = (stepcount > 0);
      break;
    }
    
    P += dP; Q.z += dQ.z; k += dk; stepcount += 1;
  }

  Q.xy += dQ.xy * stepcount;

  hitpoint = Q * (1 / k);
  
  return result;
}
 
///////////////////////// main //////////////////////////////////////////////
void main(void)
{ 
  gl_WorkGroupSize;

  ivec2 xy = ivec2(gl_GlobalInvocationID.xy);
  
  ivec2 viewport = textureSize(colormap, 0).xy;//imageSize(dest);
  
  float depth = texelFetch(depthmap, xy, 0).r;
  
  vec3 fragcolor = vec3(0);

  vec4 srccolor = texelFetch(colormap, xy, 0);
  
  Material material = unpack_material(srccolor, texelFetch(rt1map, xy, 0)); 

  if (depth < 0.99 && material.roughness < 0.4 && srccolor.a < 0.85)
  {
    vec3 position = view_position(scene.proj, scene.invproj, xy, viewport, depth);
    vec3 normal = view_normal(scene.view, texelFetch(normalmap, xy, 0).xyz);

    vec3 direction = normalize(position);
    vec3 reflection = normalize(reflect(direction, normal));

    mat4 proj = mat4(vec4(0.5*viewport.x, 0, 0, 0), vec4(0, 0.5*viewport.y, 0, 0), vec4(0, 0, 1, 0), vec4(0.5*viewport, 0, 1)) * scene.proj;
    
    vec2 hitpixel = vec2(0);
    vec3 hitpoint  = vec3(0);
    float stepcount = 0;
    
    if (traceray(position, reflection, proj, hitpixel, hitpoint, stepcount))
    {
      float edgefade = 1 - 10 * max(max(abs(hitpixel.x / viewport.x - 0.5), abs(hitpixel.y / viewport.y - 0.5)) - 0.4, 0);
      float depthfade = 1 - 1000 * max(depth - 0.989, 0);
      float distancefade = 1 - min(max(stepcount / MaxSteps, distance(hitpoint, position) / MaxDistance), 1);
      float roughnessfade = 1 - min(4 * material.roughness, 1);

      vec4 hitcolor = texelFetch(colormap, ivec2(hitpixel), 0);

      fragcolor = depthfade * edgefade * distancefade * roughnessfade * (1 - srccolor.a) * (1 - hitcolor.a) * hitcolor.rgb * material.specular;     
    }
  }

  imageStore(dest, xy, vec4(fragcolor, 1));
}
